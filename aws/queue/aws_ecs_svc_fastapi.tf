# aws_ecs_svc_fastapi.tf

# #################################
# Variable
# #################################
locals {
  fastapi_log_id           = "/ecs/task/${var.project}-${var.env}-fastapi"
  fastapi_app_debug        = var.debug
  fastapi_ecr              = "${local.ecr_repo}:${var.svc_param.fastapi_svc.image_suffix}"
  fastapi_cpu              = var.svc_param.fastapi_svc.cpu
  fastapi_memory           = var.svc_param.fastapi_svc.memory
  fastapi_count_desired    = var.svc_param.fastapi_svc.count_desired
  fastapi_count_min        = var.svc_param.fastapi_svc.count_min
  fastapi_count_max        = var.svc_param.fastapi_svc.count_max
  fastapi_env_pool_size    = var.svc_param.fastapi_svc.container_env["pool_size"]
  fastapi_env_max_overflow = var.svc_param.fastapi_svc.container_env["max_overflow"]
  fastapi_env_worker       = var.svc_param.fastapi_svc.container_env["worker"]
  fastapi_env_pgdb_host    = aws_db_instance.postgres.address
  fastapi_env_pgdb_db      = aws_db_instance.postgres.db_name
  fastapi_env_pgdb_user    = aws_db_instance.postgres.username
  fastapi_env_pgdb_pwd     = aws_db_instance.postgres.password
  fastapi_env_kafka_topic  = var.kafka_topic
  fastapi_scale_cpu        = var.scale_cpu
}

# #################################
# IAM: Execution Role
# #################################
# assume role
resource "aws_iam_role" "execution_role_fastapi" {
  name               = "${var.project}-${var.env}-execution-role-fastapi"
  assume_role_policy = data.aws_iam_policy_document.assume_role_ecs.json
}

# policy attachment: exec role
resource "aws_iam_role_policy_attachment" "ecs_task_execution_role_policy_fastapi" {
  role       = aws_iam_role.execution_role_fastapi.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

# #################################
# IAM: Task Role
# #################################
resource "aws_iam_role" "task_role_fastapi" {
  name               = "${var.project}-${var.env}-task-role-fastapi"
  assume_role_policy = data.aws_iam_policy_document.assume_role_ecs.json
}

# policy: fastapi to msk
data "aws_iam_policy_document" "fastapi_to_msk" {
  # Connect
  statement {
    sid    = "KafkaClusterConnectAndDescribe"
    effect = "Allow"
    actions = [
      "kafka-cluster:Connect",
      "kafka-cluster:DescribeCluster",
      "kafka-cluster:DescribeClusterDynamicConfiguration",
    ]
    resources = [
      "arn:aws:kafka:${var.aws_region}:${data.aws_caller_identity.current.account_id}:cluster/${aws_msk_cluster.kafka.cluster_name}/*"
    ]
  }

  # Topic
  statement {
    sid    = "KafkaTopicAccess"
    effect = "Allow"
    actions = [
      "kafka-cluster:DescribeTopic",
      "kafka-cluster:DescribeTopicDynamicConfiguration",
      "kafka-cluster:WriteData",
    ]
    resources = [
      "arn:aws:kafka:${var.aws_region}:${data.aws_caller_identity.current.account_id}:topic/${aws_msk_cluster.kafka.cluster_name}/*/telemetry"
    ]
  }
}

resource "aws_iam_policy" "fastapi_msk" {
  name   = "${var.project}-${var.env}-fastapi-msk"
  policy = data.aws_iam_policy_document.fastapi_to_msk.json
}

resource "aws_iam_role_policy_attachment" "fastapi_msk" {
  role       = aws_iam_role.task_role_fastapi.name
  policy_arn = aws_iam_policy.fastapi_msk.arn
}


# ##############################
# Security Group
# ##############################
resource "aws_security_group" "fastapi" {
  name        = "${var.project}-${var.env}-sg-fastapi"
  description = "App security group"
  vpc_id      = aws_vpc.main.id

  ingress {
    description     = "Allow load balancer to ingress"
    from_port       = 8000
    to_port         = 8000
    protocol        = "tcp"
    security_groups = [aws_security_group.alb.id] # limit source: alb
  }

  # Egress to vpc only
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${var.project}-${var.env}-sg-fastapi"
  }
}

# #################################
# ECS: Task Definition
# #################################
resource "aws_ecs_task_definition" "ecs_task_fastapi" {
  family                   = "${var.project}-${var.env}-task-fastapi"
  requires_compatibilities = ["FARGATE"]
  network_mode             = "awsvpc"
  cpu                      = local.fastapi_cpu
  memory                   = local.fastapi_memory
  execution_role_arn       = aws_iam_role.execution_role_fastapi.arn
  task_role_arn            = aws_iam_role.task_role_fastapi.arn

  # method: json file
  # container_definitions = file("./container/fastapi.json")

  # method: template file
  container_definitions = templatefile("${path.module}/container/fastapi.tftpl", {
    image           = local.fastapi_ecr
    cpu             = local.fastapi_cpu
    memory          = local.fastapi_memory
    awslogs_group   = local.fastapi_log_id
    region          = var.aws_region
    project         = var.project
    env             = var.env
    debug           = local.fastapi_app_debug
    pgdb_host       = local.fastapi_env_pgdb_host
    pgdb_db         = local.fastapi_env_pgdb_db
    pgdb_user       = local.fastapi_env_pgdb_user
    pgdb_pwd        = local.fastapi_env_pgdb_pwd
    pool_size       = local.fastapi_env_pool_size
    max_overflow    = local.fastapi_env_max_overflow
    worker          = local.fastapi_env_worker
    redis_host      = aws_elasticache_replication_group.redis.primary_endpoint_address
    redis_port      = aws_elasticache_replication_group.redis.port
    kafka_bootstrap = aws_msk_cluster.kafka.bootstrap_brokers_sasl_iam
    kafka_topic     = local.fastapi_env_kafka_topic
  })

  tags = {
    Name = "${var.project}-${var.env}-task-fastapi"
  }
}

# #################################
# ECS: Service
# #################################
resource "aws_ecs_service" "ecs_svc_fastapi" {
  name    = "${var.project}-${var.env}-service-fastapi"
  cluster = aws_ecs_cluster.ecs_cluster.id

  # task
  task_definition  = aws_ecs_task_definition.ecs_task_fastapi.arn
  desired_count    = local.fastapi_count_desired
  launch_type      = "FARGATE"
  platform_version = "LATEST"

  # network
  network_configuration {
    security_groups  = [aws_security_group.fastapi.id]
    subnets          = [for subnet in aws_subnet.private : subnet.id]
    assign_public_ip = false # disable public ip
  }

  # lb
  load_balancer {
    target_group_arn = aws_alb_target_group.fastapi_svc.arn
    container_name   = "fastapi"
    container_port   = 8000
  }

  tags = {
    Name = "${var.project}-${var.env}-service-fastapi"
  }

  depends_on = [
    aws_cloudwatch_log_group.log_group_fastapi,
    aws_vpc_endpoint.ecr_api,
    aws_vpc_endpoint.ecr_dkr,
    aws_vpc_endpoint.s3,
  ]
}

# #################################
# Service: Scaling policy
# #################################
resource "aws_appautoscaling_target" "scaling_target_fastapi" {
  service_namespace  = "ecs"
  resource_id        = "service/${aws_ecs_cluster.ecs_cluster.name}/${aws_ecs_service.ecs_svc_fastapi.name}"
  scalable_dimension = "ecs:service:DesiredCount"
  min_capacity       = local.fastapi_count_min
  max_capacity       = local.fastapi_count_max
}

# scaling policy: cpu
resource "aws_appautoscaling_policy" "scaling_cpu_fastapi" {
  name               = "${var.project}-scale-cpu-fastapi"
  resource_id        = aws_appautoscaling_target.scaling_target_fastapi.resource_id
  scalable_dimension = aws_appautoscaling_target.scaling_target_fastapi.scalable_dimension
  service_namespace  = aws_appautoscaling_target.scaling_target_fastapi.service_namespace
  policy_type        = "TargetTrackingScaling"

  target_tracking_scaling_policy_configuration {
    predefined_metric_specification {
      predefined_metric_type = "ECSServiceAverageCPUUtilization"
    }
    target_value       = local.fastapi_scale_cpu # cpu%
    scale_in_cooldown  = 30
    scale_out_cooldown = 30
  }
}

resource "aws_appautoscaling_policy" "scaling_memory_fastapi" {
  name               = "${var.project}-scale-memory-fastapi"
  resource_id        = aws_appautoscaling_target.scaling_target_fastapi.resource_id
  scalable_dimension = aws_appautoscaling_target.scaling_target_fastapi.scalable_dimension
  service_namespace  = aws_appautoscaling_target.scaling_target_fastapi.service_namespace
  policy_type        = "TargetTrackingScaling"

  target_tracking_scaling_policy_configuration {
    predefined_metric_specification {
      predefined_metric_type = "ECSServiceAverageMemoryUtilization"
    }
    target_value       = 40
    scale_in_cooldown  = 60
    scale_out_cooldown = 60
  }
}

# #################################
# CloudWatch: log group
# #################################
resource "aws_cloudwatch_log_group" "log_group_fastapi" {
  name              = local.fastapi_log_id
  retention_in_days = 7
  kms_key_id        = aws_kms_key.cloudwatch_log.arn

  tags = {
    Name = local.fastapi_log_id
  }
}

# #################################
# Monitoring: cup alarm
# #################################
resource "aws_cloudwatch_metric_alarm" "ecs_fastapi_high_cpu" {
  alarm_name          = "${var.project}-${var.env}-ecs-fastapi-high-cpu"
  namespace           = "AWS/ECS"
  metric_name         = "CPUUtilization"
  comparison_operator = "GreaterThanThreshold"
  statistic           = "Average"
  threshold           = 50
  period              = 60 # period in seconds
  evaluation_periods  = 2  # number of periods to compare with threshold.  

  dimensions = {
    ClusterName = aws_ecs_cluster.ecs_cluster.name
    ServiceName = aws_ecs_service.ecs_svc_fastapi.name
  }

  alarm_description = "High CPU on ECS FastAPI service"
}
